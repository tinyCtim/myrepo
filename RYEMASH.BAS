10 WIDTH 200
20 REM RYEMASH.BAS - 12/10,11,12/19 - LRB - RYE MASH CHALLENGE
30 REM FORTRAN COLORING BOOK
40 DIM AR(19,19)
50 REM INITIALIZE TEMPERATURE ARRAY
60 FOR J=0 TO 19:AR(0,J)=100:AR(19,J)=32:NEXT
70 REM TOP AND BOTTOM ROWS ARE HOT AND FREEZING
80 FOR I=19 TO 10 STEP -1:AR(I,0)=32:AR(I,19)=32:NEXT
90 REM BOTTOM HALF OF LEFT AND RIGHT COLUMNS ARE FREEZING
100 FOR I=0 TO 10:AR(I,0)=-6.8*I+100:AR(I,19)=-6.8*I+100:NEXT
110 REM TOP HALF OF LEFT AND RIGHT COLUMNS GRADUALLY GET COLDER
120 FOR I=7 TO 12:AR(I,7)=212:AR(I,12)=212:NEXT
130 REM LEFT AND RIGHT INNER PIPE IS STEAMING
140 FOR J=7 TO 12:AR(7,J)=212:AR(12,J)=212:NEXT
150 REM TOP AND BOTTOM INNER PIPE IS STEAMING
160 REM MAIN LOOP ENTRY POINT
170 DELTA=0
180 FOR I=1 TO 6:REM TOP SECTION
190 FOR J=1 TO 18
200 GOSUB 510
210 NEXT:NEXT
220 FOR I=13 TO 18:REM BOTTOM SECTION
230 FOR J=1 TO 18
240 GOSUB 510
250 NEXT:NEXT
260 FOR I=7 TO 12:REM MIDDLE LEFT SECTION
270 FOR J=1 TO 6
280 GOSUB 510
290 NEXT:NEXT
300 FOR I=7 TO 12:REM MIDDLE RIGHT SECTION
310 FOR J=13 TO 18
320 GOSUB 510
330 NEXT:NEXT
340 IF ABS(DELTA)<.1 THEN 380
350 REM GET OUT IF CHANGE IN TEMPERATURES IS SMALL
360 REM PRINT DELTA;
370 GOTO 160:REM OTHERWISE, LOOP BACK
380 REM DUMP AR
390 PRINT
400 FOR I=0 TO 19
410 FOR J=0 TO 19
420 FOO$=STR$(INT(AR(I,J)))
430 FOO$=MID$(FOO$,2)
440 IF LEN(FOO$)=2 THEN FOO$="0"+FOO$
450 IF LEN(FOO$)=1 THEN FOO$="00"+FOO$
460 PRINT FOO$+" ";
470 NEXT
480 PRINT
490 NEXT
500 SYSTEM
510 REM CALCULATE AVERAGE SUBROUTINE
520 ARS=AR(I,J):REM SAVE OLD VALUE
530 REM "MAGIC FORMULA"
540 AR(I,J)=(AR(I-1,J)+AR(I,J+1)+AR(I+1,J)+AR(I,J-1))/4
550 DELTA=DELTA+ARS-AR(I,J)
560 RETURN
